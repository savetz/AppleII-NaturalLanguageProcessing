-------------------------------------------- SCREEN 000
( PARSING EXPERIMENTS   LRP  7/25/85 )  
                                        
( PREPROCESSOR )                        
                                        
145 155 THRU                            
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 001
( PREPROCESSOR   LRP  7/25/85 )         
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 005
( PREPROCESSOR  LRP  7/25/85 )          
                                        
: PREPROCESSOR ;                        
                                        
VARIABLE IBUFF 50 ALLOT                 
                                        
VARIABLE IPTR                           
                                        
VARIABLE WORDBUFF 150 ALLOT             
                                        
VARIABLE CHAR-PTR                       
                                        
VARIABLE NWFLAG                         
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 006
( PREPROCESSOR  LRP  7/25/85 )          
                                        
: INIT-IBUFF ( -- )                     
   0 IBUFF !                            
   WORDBUFF CHAR-PTR !                  
   0 IPTR !                             
   1 NWFLAG ! ;                         
                                        
INIT-IBUFF                              
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 007
( PREPROCESSOR   LRP  7/25/85 )         
                                        
: #WORDS  ( -- )                        
   IBUFF @ ;                            
                                        
: WORD-PTR   ( index -- adr )           
   2 * 2+ IBUFF + ;                     
                                        
: WORD-ADR   ( index -- adr )           
   WORD-PTR @ ;                         
                                        
: INC-CHAR-PTR ( -- )                   
   1 CHAR-PTR +! ;                      
                                        
: INC-COUNT   ( -- )                    
   IPTR @                               
   WORD-ADR                             
   DUP C@ 1+ SWAP C! ;                  
                                        
: INC-IPTR ( -- )                       
   1 IPTR +!                            
   1 IBUFF +! ;                         
                                        
                                        
-------------------------------------------- SCREEN 008
( PREPROCESSOR   LRP  7/25/85 )         
                                        
: !CHAR   ( char -- )                   
   CHAR-PTR @ C!                        
   INC-CHAR-PTR                         
   0 NWFLAG ! ;                         
                                        
: ENDWORD  ( -- )                       
   0 !CHAR ;                            
                                        
: NEWWORD  ( char -- )                  
   INC-IPTR                             
   CHAR-PTR @ IPTR @ WORD-PTR !         
   0 !CHAR                              
   !CHAR ;                              
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 009
( PREPROCESSOR   LRP  7/25/85 )         
                                        
: DEC-IPTR  ( -- )                      
   IPTR @                               
   IF                                   
     -1 IBUFF +!                        
     -1 IPTR +!                         
   ELSE                                 
     BEEP                               
   THEN ;                               
                                        
: DEC-CHAR-PTR  ( -- )                  
   -1 CHAR-PTR +! ;                     
                                        
: DEC-COUNT  ( -- )                     
   IPTR @                               
   WORD-ADR                             
   DUP C@ 1- SWAP C! ;                  
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 010
( PREPROCESSOR   LRP  7/25/85 )         
                                        
: BACKSPACE   ( -- )                    
   DEC-COUNT                            
   IPTR @                               
   WORD-ADR                             
   C@  IF                               
         DEC-CHAR-PTR                   
       ELSE                             
         DEC-IPTR                       
       THEN ;                           
                                        
: CHAR   ( char -- )                    
   NWFLAG @ IF                          
              NEWWORD                   
            ELSE                        
              !CHAR                     
            THEN                        
            INC-COUNT ;                 
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 011
( PREPROCESSOR  LRP  7/25/85 )          
                                        
: DELETE?   ( char -- f )               
   DUP     8 =                          
   SWAP  127 =                          
             OR ;                       
                                        
: SPACE?   ( char -- f )                
   32 = ;                               
                                        
: DIGIT?  ( char -- f )                 
   DUP     48 <                         
   SWAP    57 >                         
              OR NOT ;                  
                                        
: RET?   ( char -- f )                  
  13 = ;                                
                                        
: ESC?   ( char -- f )                  
  27 = ;                                
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 012
( PREPROCESSOR   LRP  7/25/85 )         
                                        
: UC?   ( char -- f )                   
    DUP   65 <                          
    SWAP  90 >                          
             OR NOT ;                   
                                        
: LC?  ( char -- f )                    
    DUP   97 <                          
    SWAP 122 >                          
             OR NOT ;                   
                                        
: CHAR?  ( char -- f )                  
    DUP  UC?                            
    SWAP LC?                            
         OR ;                           
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 013
( PREPROCESSOR   LRP  7/25/85 )         
                                        
: PUNC1   ( char -- f )                 
    DUP   33 <                          
    SWAP  47 >                          
             OR NOT ;                   
                                        
: PUNC2  ( char -- f )                  
    DUP   58 <                          
    SWAP  63 >                          
             OR NOT ;                   
                                        
: PUNC?  ( char -- f )                  
    DUP  PUNC1                          
    SWAP PUNC2                          
         OR ;                           
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 014
( PREPROCESSOR   LRP  7/25/85 )         
                                        
: INPUT   ( -- )                        
   BEGIN                                
     KEY   DUP EMIT                     
     DUP  CHAR?    IF DUP CHAR     ELSE 
     DUP  DELETE?  IF BACKSPACE    ELSE 
     DUP  SPACE?   IF 1 NWFLAG !   ELSE 
     DUP  DIGIT?   IF DUP CHAR     ELSE 
     DUP  PUNC?    IF 1 NWFLAG !        
                      DUP CHAR          
                      1 NWFLAG !   ELSE 
     DUP RET?      IF DROP 0       ELSE 
     DUP ESC?      IF DROP 0       ELSE 
                   THEN THEN THEN THEN  
                   THEN THEN THEN       
  NOT                                   
  UNTIL ;                               
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 015
( PREPROCESSOR    LRP  7/26/85 )        
                                        
: QQ   ( -- )                           
    CR                                  
    #WORDS 0 DO                         
                I 1+ WORD-ADR 1+ C@     
                PUNC? NOT IF            
                            SPACE       
                          THEN          
                I 1+ WORD-ADR           
                COUNT TYPE              
             LOOP                       
             CR ;                       
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
