-------------------------------------------- SCREEN 000
( CGRAPH II LOAD SCREEN  LRP  1/27/85)  
                                        
153 160 THRU                            
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 013
( #LIST    LRP   1/26/85 )              
                                        
                                        
: CGRAPH ;                              
                                        
: FALSE   0 ;                           
                                        
: TRUE    1 ;                           
                                        
: #LIST   0 BEGIN OVER @ WHILE 1+       
     SWAP @ SWAP REPEAT                 
     SWAP DROP ;                        
                                        
: POS-DATA   4 + ;                      
                                        
: CON-PTR   2+ ;                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 014
( POS  LRP  1/26/85 )                   
                                        
: POS                                   
    CREATE                              
    DOES> ;                             
                                        
POS NOUN                                
POS VERB                                
POS ADJECTIVE                           
POS ADVERB                              
POS DET                                 
                                        
: POS?  ( adr1 -- adr2 )                
   POS-DATA @ ;                         
                                        
: .POS   ( adr -- )                     
    POS? NFA ID. ;                      
                                        
: CONCEPT?  ( adr1 -- adr2 )            
    CON-PTR @ ;                         
                                        
: .CONCEPT   ( adr -- )                 
     CONCEPT? NFA ID. ;                 
                                        
-------------------------------------------- SCREEN 015
( CONCEPTS      LRP     1/11/85 )       
                                        
: CONCEPT  ( cname -- )                 
    CREATE                              
      0 , 0 , 0 ,                       
    DOES> ;                             
                                        
: GRAPHS   ( adr -- adr1 )              
    0 + ;                               
                                        
: SUPERTYPES  ( adr -- adr1 )           
    2 + ;                               
                                        
: SUBTYPES    ( adr -- adr1 )           
    4 + ;                               
                                        
EXIT                                    
                                        
CONCEPT is a defining word to create    
concept headers in the FORTH            
dictionary -- e.g. CONCEPT UNICORN.     
UNICORN GRAPHS puts the address of the  
pointer to the graph list on the stack. 
                                        
-------------------------------------------- SCREEN 016
( CGRAPHS      LRP    1/11/85 )         
                                        
: VOCAB   ( -- )                        
    CREATE                              
      0 ,    ( Ptr to concepts list. )  
    DOES> ;                             
                                        
                                        
EXIT                                    
                                        
VOCAB is a defining word to enter       
lexicon entries into the FORTH          
dictionary -- e.g. VOCAB unicorn.       
When the new word "unicorn" is executed 
the address of the head of the CONCEPTS 
list is put on the stack.               
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 017
( PUT GET     LRP    1/16/85 )          
                                        
: GETLINK  ( n -- adr of link )         
    DUP HERE SWAP 0 FILL                
        HERE SWAP ALLOT ;               
                                        
: PUTLINK  ( pos ptr list.adr link.adr  
             -- )                       
   ( Link le to list; store ptr in )    
   ( ptr field of link. )               
   DDUP SWAP @ SWAP ! ( link <- <list>) 
   DUP ROT !          ( list <- link )  
   SWAP OVER CON-PTR !                  
   POS-DATA ! ;                         
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 018
( IS-A     LRP    1/13/85 )             
                                        
                                        
: MEANS  ( adr -- )                     
     FIND 2+                            
     SWAP 6 GETLINK PUTLINK ;           
                                        
EXIT                                    
                                        
MEANS links concept pointers to         
lexicon entries.                        
Example, NOUN cat MEANS ANIMAL. The     
lexicon entry can be linked to multiple 
concepts.                               
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 019
( IS-A     LRP    1/13/85 )             
                                        
: DATA  2+ ;                            
                                        
: MAKELINK  ( ptr list.adr link.adr --) 
   ( Link le to list; store ptr in )    
   ( ptr field of link.)                
   DDUP SWAP @ SWAP ! ( link <- <list>) 
   DUP ROT !          ( list <- link )  
   DATA ! ;           ( ptr <- data )   
                                        
: IS-A   ( adr -- )                     
   FIND 2+                              
   DDUP SUBTYPES 4 GETLINK MAKELINK     
   SWAP SUPERTYPES 4 GETLINK MAKELINK ; 
                                        
EXIT                                    
                                        
IS-A links together the                 
SUBTYPE/SUPERTYPE lists. Example,       
FELINE IS-A ANIMAL.                     
                                        
                                        
                                        
-------------------------------------------- SCREEN 020
( EXAMPLE ENTRIES    LRP   1/16/85 )    
                                        
                                        
CONCEPT FELINE                          
CONCEPT MAMMAL                          
CONCEPT ANIMATE                         
CONCEPT EARTHMOVER                      
CONCEPT INANIMATE                       
CONCEPT CANINE                          
                                        
FELINE IS-A MAMMAL                      
MAMMAL IS-A ANIMATE                     
EARTHMOVER IS-A INANIMATE               
CANINE IS-A MAMMAL                      
                                        
VOCAB cat                               
VOCAB tabby                             
VOCAB dog                               
                                        
NOUN cat MEANS FELINE                   
NOUN cat MEANS EARTHMOVER               
NOUN dog MEANS CANINE                   
                                        
                                        
-------------------------------------------- SCREEN 021
( MEANS?   LRP   1/27/85 )              
                                        
: MEANS?   ( adr -- )                   
    ( Given address of word in lexicon,)
    ( Traverse concept list. )          
    CR                                  
    BEGIN                               
       DUP @                            
    WHILE                               
       @ DUP          .CONCEPT          
         DUP 5 SPACES .POS              
         CR                             
    REPEAT DROP ;                       
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 022
( SUPTRAV     LRP   1/27/85 )           
                                        
VARIABLE T     VARIABLE P               
                                        
: LCHILD   ( adr1 -- adr2 )             
    @ ;                                 
                                        
: RCHILDP   ( adr1 -- adr2 )            
    CONCEPT? SUPERTYPES  ;              
                                        
: VISIT   ( adr -- )                    
    CONCEPT? NFA ID. CR ;               
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 023
( SUPTRAV     LRP   1/27/85 )           
                                        
: LEFTTRAV   ( -- n1 n2 ... n )         
    ( Assumes address of concept is)    
    ( stored in P. )                    
    BEGIN                               
      P @ LCHILD                        
    WHILE                               
      P @ LCHILD P !  P @               
    REPEAT ;                            
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 024
( TRAVERSE    LRP    1/27/85 )          
                                        
1 CONSTANT EXIT-FLG                     
                                        
: STEPRT   ( -- flg )                   
    BEGIN                               
      P @ VISIT                         
      P @ RCHILDP P !                   
      P @ IF                            
             EXIT-FLG NOT EXIT-FLG      
          ELSE                          
             DEPTH IF                   
                     P !                
                   ELSE                 
                     EXIT-FLG EXIT-FLG  
                   THEN                 
          THEN                          
    UNTIL  ;                            
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 025
( SUPTRAV     LRP   1/27/85 )           
                                        
: SUPTRAV    ( adr -- )                 
     CR SUPERTYPES T !                  
     T @ IF                             
           T @  P !                     
           BEGIN                        
             LEFTTRAV                   
             STEPRT                     
           UNTIL                        
         ELSE                           
           DROP                         
         THEN ;                         
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 030
( TRAVERSE    LRP    1/27/85 )          
                                        
: STEPRT   ( -- )                       
    BEGIN                               
      0                                 
      P @ VISIT                         
      P @ RCHILDP P !                   
      P @ NOT                           
    WHILE                               
      DROP                              
      DEPTH                             
        IF                              
           P !                          
        ELSE                            
           1 EXIT                       
        THEN                            
    REPEAT ;                            
                                        
                                        
                                        
                                        
                                        
                                        
                                        
